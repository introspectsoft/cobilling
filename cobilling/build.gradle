/*
 * Copyright (c) 2020. Jason Burgess
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply from: "${rootProject.projectDir}/buildsystem/config.gradle"

apply plugin: "com.android.library"
apply plugin: "kotlin-android"
apply plugin: "kotlin-android-extensions"
apply plugin: "org.jetbrains.dokka"
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

project.buildDir = "../builds/"

android {
    compileSdkVersion configuration.compileSdkVersion
    buildToolsVersion configuration.buildToolsVersion

    defaultConfig {
        minSdkVersion configuration.minimumSdkVersion
        targetSdkVersion configuration.targetSdkVersion
        versionCode buildVersionCode()
        versionName buildVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.4.0-alpha01'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.billingclient:billing-ktx:2.2.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.0-alpha03'
    implementation 'com.jakewharton.timber:timber:4.7.1'

    testImplementation 'junit:junit:4.13'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

}

dokka {
    configuration {
        androidVariants = ["debug", "release"]
    }
    outputFormat = "jekyll"
    outputDirectory = "../builds/outputs/markdown"
}

task dokkaHtml(type: org.jetbrains.dokka.gradle.DokkaTask) {
    configuration {
        androidVariants = ["debug", "release"]
    }
    outputFormat = "html"
    outputDirectory = "../builds/outputs/javadoc"
}

task javadocJar(type: Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Assembles Kotlin docs with Dokka"
    archiveClassifier = 'javadoc'
    destinationDirectory = project.buildDir
    from(tasks.dokkaHtml)
}

task sourcesJar(type: Jar) {
    group = JavaBasePlugin.DOCUMENTATION_GROUP
    description = "Assembles sources jar"
    destinationDirectory = project.buildDir
    archiveClassifier = 'sources'
    from(android.sourceSets.main.java.srcDirs)
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = 'ml.introspectsoft.cobilling'
            artifactId = 'cobilling'
            version = buildVersionName()

            artifact sourcesJar
            artifact javadocJar
            afterEvaluate {
                from components.release
            }
            pom {
                name = 'cobilling'
                description = 'Kotlin coroutines wrappers for Google Play Billing API '
                url = 'https://introspectsoft.github.io/cobilling/'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'introspectdev'
                        name = 'Jason Burgess'
                        email = 'introspect.dev@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:https://github.com/introspectsoft/cobilling.git'
                    url = 'https://github.com/introspectsoft/cobilling'
                }
            }
        }
    }
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            url = "${project.buildDir}/outputs/maven-repo"
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.getProperty('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['release']

    pkg {
        repo = 'introspectsoft-maven'
        name = 'ml.introspectsoft.cobilling:cobilling'
        licenses = ['Apache-2.0']
        userOrg = 'introspectsoft'
        vcsUrl = 'https://github.com/introspectsoft/cobilling.git'
        version {
            name = buildVersionName()
            vcsTag = buildVersionName()
            desc = "CoBilling library " + buildVersionName()
            released = new Date()
        }
    }
}
